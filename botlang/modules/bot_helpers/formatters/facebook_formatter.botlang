(module "facebook-formatter"

    [define format-template
        (function (payload)
            (make-dict (list
                (cons "attachment"
                    (make-dict (list
                        (cons "type" "template")
                        (cons "payload" payload)
                    ))
                )
            ))
        )
    ]

    [define generic-template
        (function (elements)
            (format-template
                (make-dict (list
                    (cons "template_type" "generic")
                    (list "elements" elements)
                ))
            )
        )
    ]

    [define button-template
        (function (message buttons)
            (format-template
                (make-dict (list
                    (cons "template_type" "button")
                    (cons "text" message)
                    (list "buttons" buttons)
                ))
            )
        )
    ]

    [define list-template
        (function (elements)
            (format-template
                (make-dict (list
                    (cons "template_type" "list")
                    (cons "top_element_style" "compact")
                    (list "elements" elements)
                ))
            )
        )
    ]

    [define format-facebook-simple-list
        (function (heading elements)
            (list
                heading
                (list-template
                    (map
                        (function (element)
                            (make-dict (list
                                (cons "title" (get element 0))
                                (cons "subtitle" (get element 1))
                            ))
                        )
                        elements
                    )
                )
            )
        )
    ]

    [define separate-list
        (function (list-to-separate num-sub-lists)
            (fold
                (list)
                (function (acc next)
                    [define index (get next 0)]
                    [define element (get next 1)]
                    (if (equal? (mod index num-sub-lists) 0)
                        (append acc (list (list element)))
                        (append
                            (init acc)
                            (list
                                (append (last acc) (list element))
                            )
                        )
                    )
                )
                (enumerate list-to-separate)
            )
        )
    ]

    [define format-option-buttons
        (function (message options)
            (button-template
                message
                (map
                    [function (option)
                        (make-dict (list
                            (cons "type" "postback")
                            (cons "title" (get option 1))
                            (cons "payload" (get option 0))
                        ))
                    ]
                    options
                )
            )
        )
    ]

    [define format-facebook-options
        (function (message all-options)
            (map
                [function (options-group)
                    (format-option-buttons
                        (if (equal? (get options-group 0) 0)
                            message
                            "\u2063"
                        )
                        (get options-group 1)
                    )
                ]
                (enumerate (separate-list all-options 3))
            )
        )
    ]

    [define format-facebook-link
        (function (title link-url image-url)
            (generic-template (list
                (make-dict (list
                    (cons "title" title)
                    (cons "item_url" link-url)
                    (cons "image_url" image-url)
                ))
            ))
        )
    ]

    (provide
        format-facebook-options
        format-facebook-link
        format-facebook-simple-list
        separate-list
    )
)